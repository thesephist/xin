; str standard library

(: (blank? s)
   (= s ''))

(: (reverse s)
   (if (blank? s)
     ''
     (+ (reverse (str::slice s 1 (str::size s)))
        (str::get s 0))))

(: (reduce s f acc)
   (do
     (: max (str::size s))
     (: (sub i acc)
        (if (< i max)
          (sub (+ i 1)
               (f (str::get s i) acc i))
          acc))
     (sub 0 acc)))

; because we'll use map as a function name here,
; alias the buitlin map function
(: map::new map)
(: (map s f)
   (reduce s
           (: (g c acc i)
              (str::add! acc (f c i)))
           ''))

(: (filter s f)
   (reduce s
           (: (g c acc i)
              (if (f c i)
                (str::add! acc c)
                acc))
           ''))

(: (join v pad)
   (if (vec::empty? v)
     ''
     (vec::reduce (vec::tail v)
                  (: (f s acc)
                     (+ acc (+ pad s)))
                  (vec::head v))))

(: (index s sub)
   ((: (rec i)
       (if (> i (str::size s))
         -1
         (if (= (str::slice s i (+ i (str::size sub))) sub)
           i
           (rec (+ i 1)))))
    0))

(: (has? s sub)
   (> (index s sub) -1))

(: (prefix? s sub)
   (= sub (str::slice s
                      0
                      (str::size sub))))

(: (suffix? s sub)
   (= sub (str::slice s
                      (- (str::size s) (str::size sub))
                      (str::size s))))

; utility to check ascii ranges
(: (is-in-range n min max)
   (& (>= n min) (<= n max)))

(: (digit? s)
   (is-in-range (str::enc s) 48 57))

(: (upper? s)
   (is-in-range (str::enc s) 65 90))

(: (lower? s)
   (is-in-range (str::enc s) 97 122))

(: (letter? s)
   (| (upper? s) (lower? s)))

(: (upcase s)
   (map s
        (: (f c)
           (if (lower? c)
             (str::dec (- (str::enc c) 32))
             c))))

(: (downcase s)
   (map s
        (: (f c)
           (if (upper? c)
             (str::dec (+ (str::enc c) 32))
             c))))

(: (pad-start s cap fill)
   (if (< (str::size s) cap)
     (do (: diff (- cap (str::size s)))
       (+ (str::slice (* fill (inc (/ diff (str::size fill)))) 0 diff)
          s))
     s))

(: (pad-end s cap fill)
   (if (< (str::size s) cap)
     (do (: diff (- cap (str::size s)))
       (+ s
          (str::slice (* fill (inc (/ diff (str::size fill)))) 0 diff)))
     s))

; optimized str::replace with minimal copying / O(n) searching
(: (replace s old new)
   ((: (sub pref suff)
       (if (= (: idx (index suff old)) -1)
         (+ pref suff)
         (sub (+ pref
                 (+ (str::slice suff 0 idx)
                    new))
              (str::slice suff
                          (+ idx (str::size old))
                          (str::size suff)))))
    '' s))

; in the special case of (split s '')
; we instead split at every byte character
(: (split-into-chars s)
   (reduce s
           (: (f c acc)
              (vec::add! acc c))
           (vec)))
(: (split s delim)
   (if (blank? delim)
     (split-into-chars s)
     ((: (sub substr sublist)
         (if (= (: idx (index substr delim)) -1)
           (vec::add! sublist substr)
           (sub (str::slice substr
                            (+ idx (str::size delim))
                            (str::size substr))
                (vec::add! sublist
                           (str::slice substr
                                       0
                                       idx)))))
      s (vec))))

; rudimentary string formatting placeholders with just '{}'
; optimized and tail-recursive with minimal copying,
; takes after str::replace
;
; ex) (fmt 'start {} {},{} end'
;          (vec 1 2 3))
; ->  'start 1 2,3 end'
(: (fmt s items)
   ((: (sub pref suff items)
       (if (| (= (: idx (index suff '{}')) -1)
              (vec::empty? items))
         (+ pref suff)
         (sub (+ pref
                 (+ (str::slice suff 0 idx)
                    (str (vec::head items))))
              (str::slice suff
                          (+ idx 2)
                          (str::size suff))
              (vec::tail items))))
    '' s items))

(do (: esc (map::new))
  (map::set! esc '\n' '\\n')
  (map::set! esc '\r' '\\r')
  (map::set! esc '\t' '\\t')
  (map::set! esc '\\' '\\\\')
  (map::set! esc '\'' '\\\''))
(: (escape s)
   (map s (: (f c)
             (if (map::has? esc c)
               (map::get esc c)
               c))))
