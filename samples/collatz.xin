; find the longest collatz chain
; starting from a number less than N

(: (even? n)
   (factor? n 2))

(: (collatz-next n)
   (if (even? n)
     (/ n 2)
     (+ 1 (* 3 n))))

(: (collatz-seq start)
   (do (: (sub n acc)
          (do (: next (collatz-next n))
            (if (= next 1)
              (vec::add! acc next)
              (sub next (vec::add! acc next)))))
     (sub start (vec start))))

(: (longest-collatz-seq ns)
   (vec::reduce ns
                (: (f x last)
                   (do (: this-collatz (collatz-seq x))
                     (if (> (vec::size this-collatz) (vec::size last))
                       this-collatz
                       last)))
                (vec)))

(: (longest-collatz-seq-below cap)
   (longest-collatz-seq (range 1 cap 1)))

; compute the longest collatz seq below given cap
(: cap (if (< (vec::size (os::args)) 3)
         1000 ; 1000 cap default, if none given
         (int (vec::get (os::args) 2))))
(: longest-seq
   (longest-collatz-seq-below cap))
(logf 'Longest Collatz sequence below {} begins with {}, is {} numbers'
      (vec cap (vec::head longest-seq) (vec::size longest-seq)))
(log longest-seq)
